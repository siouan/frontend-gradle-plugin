import org.siouan.frontendgradleplugin.GradleTestListener

plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
    id 'com.gradle.plugin-publish'
}

repositories {
    jcenter()
    mavenCentral()
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}

tasks.named('test', Test) { t ->
    t.useJUnitPlatform()
    gradle.addListener(new GradleTestListener(logger))
}

tasks.named('jacocoTestReport') {
    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

dependencies {
    implementation gradleApi()
    implementation 'org.apache.commons:commons-compress:1.20'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testImplementation 'org.mockito:mockito-core:3.4.6'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.4.6'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.27.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

group 'org.siouan'
version '3.0.1'
description 'Build Javascript-based applications with node, npm, yarn.'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

gradlePlugin {
    plugins {
        frontend {
            id = 'org.siouan.frontend'
            implementationClass = 'org.siouan.frontendgradleplugin.FrontendGradlePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/siouan/frontend-gradle-plugin'
    vcsUrl = 'https://github.com/siouan/frontend-gradle-plugin.git'

    plugins {
        frontend {
            displayName = 'Frontend Gradle plugin'
            description = 'Build Javascript-based applications with node, npm, yarn.'
            tags = ['node', 'nodejs', 'npm', 'npx', 'yarn', 'frontend']
        }
    }
}
