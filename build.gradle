plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
    id 'com.gradle.build-scan' version '2.3'
    id 'com.gradle.plugin-publish' version '0.10.1'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    jcenter()
    mavenCentral()
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

dependencies {
    implementation gradleApi()
    implementation 'org.apache.commons:commons-compress:1.18'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.28.2'
    testImplementation 'org.assertj:assertj-core:3.12.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

group 'org.siouan'
version '1.2.0'
description 'Integrate your frontend NPM/Yarn build into Gradle.'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

gradlePlugin {
    plugins {
        frontend {
            id = 'org.siouan.frontend'
            displayName = 'Frontend Gradle plugin'
            description = 'Integrate your frontend Node/NPM/Yarn build into Gradle.'
            implementationClass = 'org.siouan.frontendgradleplugin.FrontendGradlePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/Siouan/frontend-gradle-plugin'
    vcsUrl = 'https://github.com/Siouan/frontend-gradle-plugin.git'
    tags = ['node', 'npm', 'yarn', 'frontend']
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}
